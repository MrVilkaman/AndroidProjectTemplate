apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../artifacts.gradle'
apply from: '../dependencies.gradle'
apply plugin: 'com.github.ben-manes.versions'
android {
    // Declared in dependencies.gradle in the root of the project.
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "ru.fixapp.fooproject"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            if (project.hasProperty("mytestCoverageEnabled")) {
                testCoverageEnabled = mytestCoverageEnabled
            }
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            minSdkVersion 21
//            buildConfigField "String", "HOST", "\"api.byblos.su\""
        }

        prod {
        }
    }

    sourceSets {
        // Integration tests live in src/integrationTest.
        test.java.srcDir 'src/integrationTests/java'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        // Force our version of support-annotations, we have a conflict between app and androidTest dependency resolution.
        force libraries.supportAnnotations
        force libraries.supportAppCompat
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile libraries.dagger
    apt libraries.daggerCompiler
    compile libraries.supportAnnotations
    compile libraries.supportAppCompat
    compile libraries.supportRecyclerView
    compile libraries.supportDesign

    compile libraries.butterKnife
    apt libraries.butterKnifeCompiler

    compile libraries.retrofit2
    compile libraries.retrofit2rxjava
    compile libraries.retrofit2gson
    compile libraries.okHttp3

    // Developer tools (Developer Settings)
    compile libraries.okHttpLoggingInterceptor

    compile libraries.rxBus
    compile libraries.rxBusAndroid
    compile libraries.rxJava
    compile libraries.rxAndroid
    compile libraries.rxBindingAppcompat

    compile libraries.materialDialog
    compile libraries.materialProgress

    compile libraries.simplecropview
    compile libraries.picasso
    compile libraries.picassoOkhttp

    compile project(":core")

    testCompile project(':testutils')

}